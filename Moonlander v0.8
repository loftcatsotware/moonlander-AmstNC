3 REM Moonlander v0.8 
4 SNDSTAT=2
5 SCORE=0
6 LEVEL=1:DIFF=25
7 GOSUB 800:REM TITLE
10 GOSUB 190
20 X=250
30 Y=50
40 B=-1
50 REPEAT
60 PLOT 4,X,Y:PLOT 5,X+3,Y+5:PLOT 5,X+6,Y:PLOT 5,X,Y
70 REM record old co-ord
80 OX=X:OY=Y
90 Y=Y+B
100 FOR U=1 TO 150:NEXT
110 I$=INKEY$(0)
120 IF I$="Z" THEN X=X-2
130 IF I$="X" THEN X=X+2
140 IF I$="T" THEN Y=Y+2
145 PLOT 4,OX,OY:PLOT 7,OX+3,OY+5:PLOT 7,OX+6,OY:PLOT 7,OX,OY
150 P=POINT(X,Y-1):P2=POINT(X+6,Y-1)
160 IF P>0 OR P2>0 AND Y>OY THEN GOTO 500
170 UNTIL Y=2
180 GOTO 500
185 VDU 4:END
190 REM draw landscape
200 CLS
210 X=1:Y=10
220 PLOT 4,X,Y
230 REPEAT
240 R=INT(RND(DIFF))
250 IF R=1 THEN GOSUB 280 ELSE GOSUB 310
260 UNTIL X>478
270 RETURN
275 REM DRAW A FLAT LANDSCAPE SECTION
280 X=X+INT(RND(10)+1)
290 PLOT 5,X,Y
300 RETURN
310 REM draw slope
320 X=X+INT(RND(10)+1)
330 Y=Y+INT(RND(11)-5)
340 IF Y>30 THEN Y=30
350 IF Y<1 THEN Y=1
360 PLOT 5,X,Y
370 RETURN
500 REM detect crash or land
510 IF P=1 AND P2=1 THEN GOTO 650
520 REM crash
525 VDU 17
530 PRINT TAB(20,3);"C R A S H ! ! !"
540 PRINT TAB(20,4);"G A M E O V E R"
550 PRINT TAB(20,5);"SCORE: ";SCORE;"LEVEL:";LEVEL
552 PRINT TAB(20,6);"PRESS R TO RESTART A GAME"
555 VDU 18
560 IF SNDSTAT=1 THEN SOUND 0,0,50,3:SOUND 0,0,40,3:SOUND 0,0,30,3:SOUND 0,0,20,6
570 I$=GET$
575 IF I$="R" THEN GOTO 5
580 GOTO 570
650 REM Land safely
655 SCORE=SCORE+50
660 PRINT TAB(20,3);"S A F EL A N D I N G - WELL DONE!"
665 PRINT TAB(20,4);"LEVEL ";LEVEL;" COMPLETED"
670 PRINT TAB(20,5);"PRESS N FOR NEXT LANDING........."
672 LEVEL=LEVEL+1:DIFF=LEVEL+25
674 IF SNDSTAT=1 THEN SOUND 0,0,40,3:SOUND 0,0,50,3:SOUND 0,0,60,3:SOUND 0,0,70,8
677 LET N=0
680 REPEAT
690 I$=GET$
780 UNTIL I$="N"
790 GOTO 10
800 REM Titlescreen
805 VDU 5:CLS
810 FOR Y=0 TO 6
820 PRINT TAB(1,Y);"*"
823 PRINT TAB(79,Y);"*";
825 NEXT Y
840 FOR X=1 TO 79
850 PRINT TAB(X,0);"*";:PRINT TAB(X,7);"*";
860 NEXT X
870 VDU 17:PRINT TAB(30,2);"M O O N L A N D E R":VDU 18
875 PRINT TAB(28,3);"by Tony Kingsmill  2019"
880 PRINT TAB(21,4);"Controls: Z,X - left/rightT - Thrust"
885 PRINT TAB(27,5);"Toggle Sound (1)on (2)off"
890 PRINT TAB(29,6);"Press S key to Start"
990 I$=GET$
993 IF I$="1" THEN SNDSTAT=1
995 IF I$="2" THEN SNDSTAT=2
997 IF I$="S" THEN RETURN
999 GOTO 990
